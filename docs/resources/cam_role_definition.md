---
page_title: "visionone_cam_role_definition Resource - visionone"
subcategory: "Azure"
description: |-
  Trend Micro Vision One CAM Azure Role Definition resource. Creates a custom Azure role with the necessary permissions https://docs.trendmicro.com/en-us/documentation/article/trend-vision-one-azure-sub-required-permissions for Vision One Cloud Account Management.
---

# visionone_cam_role_definition (Resource)

Trend Micro Vision One CAM Azure Role Definition resource. Creates a custom Azure role with the [necessary permissions](https://docs.trendmicro.com/en-us/documentation/article/trend-vision-one-azure-sub-required-permissions) for Vision One Cloud Account Management.

#### Note
Feature-based permission configuration will be implemented in a future release. Currently, all standard permissions are applied regardless of selected features.
Review the permissions required to deploy resources and the permissions granted during the terraform process, [Azure required and granted permissions](https://docs.trendmicro.com/en-us/documentation/article/trend-vision-one-azure-sub-required-permissions)

## Example Usage

### Basic Usage (Single Subscription)

```terraform
resource "visionone_cam_role_definition" "cam_role_definition" {
  subscription_id = "11111111-1111-2222-aaaa-bbbbbbbbbbbb"
}
```

### Management Group Usage (Cross-Subscription Role Assignment)

```terraform
variable "all_subscription_ids" {
  type = list(string)
  default = [
    "11111111-1111-2222-aaaa-bbbbbbbbbbbb",  # Primary
    "00000000-1ea8-4822-b823-abcdefghijkl",  # Member 1
    "22222222-3333-4444-cccc-dddddddddddd"   # Member 2
  ]
}

resource "visionone_cam_role_definition" "primary" {
  subscription_id = var.all_subscription_ids[0]  # Created in primary subscription

  # Include all subscriptions in assignable_scopes to enable cross-subscription assignments
  assignable_scopes = toset([
    for sub_id in var.all_subscription_ids :
    "/subscriptions/${sub_id}"
  ])
}

# Now you can assign this role to ANY subscription in assignable_scopes
resource "visionone_cam_role_assignment" "members" {
  for_each = toset(slice(var.all_subscription_ids, 1, length(var.all_subscription_ids)))

  role_definition_id = visionone_cam_role_definition.primary.id
  principal_id       = "<service-principal-object-id>"
  subscription_id    = each.value  # Can assign to member subscriptions
}
```

### Example with Management Group Scope

```terraform
resource "visionone_cam_role_definition" "mgmt_group" {
  subscription_id = "11111111-1111-2222-aaaa-bbbbbbbbbbbb"

  # Use management group scope to cover all current and future subscriptions
  assignable_scopes = [
    "/providers/Microsoft.Management/managementGroups/my-mgmt-group"
  ]
}
```

### Example with Custom Permissions

```terraform
# Example: Custom Role Definition with Custom Permissions
# This example shows how to override the default Vision One CAM permissions
# with custom actions and data_actions

resource "visionone_cam_role_definition" "custom_permissions" {
  subscription_id = "11111111-1111-2222-aaaa-bbbbbbbbbbbb"

  # Override default actions with custom permissions
  # If not provided, defaults to standard Vision One CAM required permissions
  actions = [
    "Microsoft.Resources/subscriptions/read",
    "Microsoft.Storage/storageAccounts/read",
    "Microsoft.Storage/storageAccounts/listKeys/action",
    "Microsoft.ContainerRegistry/registries/read",
    "Microsoft.KeyVault/vaults/secrets/read"
  ]

  # Override default data actions with custom data permissions
  # If not provided, defaults to standard Vision One CAM required data permissions
  data_actions = [
    "Microsoft.KeyVault/vaults/secrets/getSecret/action",
    "Microsoft.KeyVault/vaults/keys/read",
    "Microsoft.KeyVault/vaults/secrets/readMetadata/action"
  ]
}

# Example: Partial Override - Only Custom Actions
# You can override just actions or just data_actions
resource "visionone_cam_role_definition" "custom_actions_only" {
  subscription_id = "11111111-1111-2222-aaaa-bbbbbbbbbbbb"

  # Custom actions, but data_actions will use defaults
  actions = [
    "*/read",
    "Microsoft.Storage/storageAccounts/listKeys/action"
  ]
}
```

### Example Detailed Usage
- Create multiple role definitions for different Azure subscriptions.
<details>

```terraform
variable "subscription_id_list" {
  type    = set(string)
  default = ["11111111-1111-2222-aaaa-bbbbbbbbbbbb", "00000000-1ea8-4822-b823-abcdefghijkl"]
}

resource "visionone_cam_role_definition" "cam_role_definition" {
  for_each        = var.subscription_id_list
  subscription_id = each.value
}
```

</details>
<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `subscription_id` (String) The ID of the subscription.

### Optional

- `actions` (List of String) Optional list of Azure role actions to assign to the custom role. If not provided, defaults to the standard Vision One CAM required permissions.
- `assignable_scopes` (Set of String) Set of scopes where the role can be assigned. Defaults to the subscription scope if not provided. For management group deployments, include all member subscription scopes to enable cross-subscription role assignments. Example: ["/subscriptions/sub-id-1", "/subscriptions/sub-id-2"] or ["/providers/Microsoft.Management/managementGroups/mg-id"]
- `data_actions` (List of String) Optional list of Azure role data actions to assign to the custom role. If not provided, defaults to the standard Vision One CAM required data permissions.
- `features` (Set of String) Set of features associated with the Trend Vision One CAM custom role definition. The role will include all permissions required by the specified features according to the Trend Vision One Azure required permissions documentation.

### Read-Only

- `description` (String) Description of the Trend Vision One CAM custom role definition.
- `id` (String) Unique identifier for the Trend Vision One CAM custom role definition.
- `name` (String) Name of the Trend Vision One CAM custom role definition.
- `scope` (String) Scope where the Trend Vision One CAM custom role definition applies, typically a subscription.

## Import
Will supported coming soon.